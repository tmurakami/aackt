buildscript {
    ext.versions = [kotlin: '1.3.41',]
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.10.1'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'pl.allegro.tech.build.axion-release'

scmVersion {
    tag {
        prefix = ''
    }
    hooks {
        pre 'fileUpdate', [file       : 'README.md',
                           pattern    : { v, p -> /(['\/])$v(['\/])/ },
                           replacement: { v, p -> "\$1$v\$2" }]
        pre 'fileUpdate', [file       : 'README.md',
                           pattern    : { v, p -> /(\/Kotlin-)[\d.]+/ },
                           replacement: { v, p -> "\$1${versions.kotlin}" }]
        pre 'commit', { v, p -> "Release version: $v [ci skip]" }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    version = scmVersion.version
    group = 'com.github.tmurakami.aackt'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    deps = [:]

    deps.ktlint = 'com.github.shyiko:ktlint:0.31.0'

    deps.kotlin = [stdlib: "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
                   junit4: "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin}",]

    def androidx = [:]
    deps.androidx = androidx
    androidx.arch = [core: [testing: 'androidx.arch.core:core-testing:2.1.0-rc01',],]

    def lifecycleVersion = '2.1.0-rc01'
    androidx.lifecycle = [runtime             : "androidx.lifecycle:lifecycle-runtime:$lifecycleVersion",
                          extensions          : "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion",
                          livedata            : "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion",
                          viewmodel           : "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion",
                          viewmodel_savedstate: 'androidx.lifecycle:lifecycle-viewmodel-savedstate:1.0.0-alpha01',]

    def roomVersion = '2.1.0'
    androidx.room = [runtime : "androidx.room:room-ktx:$roomVersion",
                     compiler: "androidx.room:room-compiler:$roomVersion",
                     rxjava2 : "androidx.room:room-rxjava2:$roomVersion",
                     testing : "androidx.room:room-testing:$roomVersion",]

    androidx.work = [runtime: 'androidx.work:work-runtime-ktx:2.1.0',]
}
