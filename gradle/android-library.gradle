apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 14
    }
    lintOptions {
        check 'Interoperability'
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs += ['-Xno-param-assertions']
    }
    // TODO https://issuetracker.google.com/issues/72050365
    libraryVariants.all { it.generateBuildConfigProvider.get().enabled = false }
}

configurations {
    ktlint
}

dependencies {
    ktlint deps.ktlint
    api deps.kotlin.stdlib
    testImplementation deps.kotlin.junit4
}

task ktlint(type: JavaExec, group: 'verification') {
    inputs.files fileTree(dir: 'src', include: '**/*.kt')
    outputs.dir 'src'
    description = 'Check Kotlin code style.'
    classpath = configurations.ktlint
    main = 'com.pinterest.ktlint.Main'
    args '--android', 'src/**/*.kt'
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: 'formatting') {
    inputs.files fileTree(dir: 'src', include: '**/*.kt')
    outputs.dir 'src'
    description = 'Fix Kotlin code style deviations.'
    classpath = configurations.ktlint
    main = 'com.pinterest.ktlint.Main'
    args '--android', '-F', 'src/**/*.kt'
}

dexcount {
    includeClasses = true
}

task javadoc(type: Javadoc)

dokka {
    outputFormat = 'html'
    outputDirectory = javadoc.destinationDir
    skipDeprecated = true
    externalDocumentationLink {
        url = 'https://developer.android.com/reference/'.toURL()
        // TODO https://github.com/Kotlin/dokka/issues/213
        packageListUrl = file("$rootDir/androidx-package-list").toURI().toURL()
    }
}
javadoc.dependsOn dokka

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.source
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            url 'https://github.com/tmurakami/aackt'
            inceptionYear '2018'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id 'tmurakami'
                    name 'Tsuyoshi Murakami'
                }
            }
            scm {
                url 'https://github.com/tmurakami/aackt'
                connection 'scm:git:git://github.com/tmurakami/aackt.git'
            }
        }
    }
}
